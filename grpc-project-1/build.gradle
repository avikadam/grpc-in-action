plugins {
    id 'application'
    id 'java-library'
    id 'com.google.protobuf' version '0.8.18'
    id 'idea'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

group 'org.bael.r365'
version '1.0-SNAPSHOT'

def grpcVersion = '1.45.0' // CURRENT_GRPC_VERSION
def protocVersion = '3.19.2'
def protobufJavaUtil = '3.19.4'

def javaxAnnotationVersion = '1.3.2'
def mongodbDriverVersion = '4.5.1'
def log4jVersion = '2.17.2'
def slf4jLog4j12Version = '1.7.36'
def junitVersion = '5.8.2'

println 'This is executed during the configuration phase.'

repositories {
    mavenCentral()
}

dependencies {
    //gRPC - google
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}" // note: shaded includes ssl libs
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protobufJavaUtil}"

    //Other
    implementation "javax.annotation:javax.annotation-api:${javaxAnnotationVersion}"

    //Mongodb
    implementation "org.mongodb:mongodb-driver-sync:${mongodbDriverVersion}"

    //Logging
    implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    implementation "org.slf4j:slf4j-log4j12:${slf4jLog4j12Version}"

    //junit
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

    compileOnly 'com.google.api.grpc:proto-google-common-protos:2.8.0'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

// if you have source imports issues, add the below
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/java'
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'src/main/java'
        }
    }

    test {
        java {
            srcDirs 'src/test/java'
        }
    }
}